apply plugin: 'checkstyle'
apply plugin: 'com.github.spotbugs'
apply plugin: 'pmd'

checkstyle {
    toolVersion = "8.12"
    ignoreFailures = false
    configFile = file("${rootGradleDir}/static-code-analysis/checkstyle/checkstyle.xml")
}

spotbugs {
    toolVersion = "3.1.7"
    ignoreFailures = false
    excludeFilter = file("${rootGradleDir}/static-code-analysis/spotbugs/exclude.xml")
}

pmd {
    toolVersion = "6.7.0"
    ignoreFailures = false
    ruleSetFiles = files("${rootGradleDir}/static-code-analysis/pmd/ruleset.xml")
    ruleSets = []
    rulePriority = 3
}

cpd {
    language = 'java'
    toolVersion = '6.0.0'
    minimumTokenCount = 100 // circa to 6-10 lines
}

cpdCheck {
    reports {
        text.enabled = false
        xml.enabled = true
    }
    source = sourceSets.main.allJava
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

tasks.withType(SpotBugs) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

tasks.withType(Pmd) {
    reports {
        xml.enabled false
        html.enabled true
    }
}
